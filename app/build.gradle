apply plugin: 'com.android.application'

def getVersionCode = {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    } catch (ignored) {
        return -1
    }
}

def getVersionName = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}
android {
    compileSdkVersion 33
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId 'org.opendoorstudios.ds4droid'
        minSdkVersion 21
        targetSdkVersion 33
        versionCode getVersionCode()
        versionName getVersionName()
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '25.2.9519653'
}

dependencies {
    implementation files('libs/ouya-sdk.jar')

    // TODO: Potentially drop v4 support.
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //https://github.com/dropbox/dropbox-sdk-java/releases
    api 'com.dropbox.core:dropbox-core-sdk:3.1.5'

    api 'net.sf.sevenzipjbinding:sevenzipjbinding:16.02-2.01'
    implementation 'net.sf.sevenzipjbinding:sevenzipjbinding-linux-arm64:16.02-2.01'
}
