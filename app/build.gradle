apply plugin: 'com.android.application'

def getVersionCode = {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'tag', '--list'
            standardOutput = code
        }
        return code.toString().split("\n").size()
    } catch (ignored) {
        return -1
    }
}

def getVersionName = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}
android {
    compileSdkVersion 27
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId 'org.opendoorstudios.ds4droid'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName()
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation files('libs/ouya-sdk.jar')

    implementation 'com.android.support:support-v4:27.1.1' //api?

    //https://github.com/dropbox/dropbox-sdk-java/releases
    api 'com.dropbox.core:dropbox-core-sdk:3.0.9' //3.1.0

    api 'net.sf.sevenzipjbinding:sevenzipjbinding:9.20-2.00beta'
    implementation 'net.sf.sevenzipjbinding:sevenzipjbinding-linux-arm:9.20-2.00beta'
}
